r/b/intIsBetween n/n n/a n/b
    # Checks whether N is within the range A and B, including endpoints
    new n/t1
    set n/t1 sub n/n n/a
    new n/t2
    set n/t2 sub n/n n/b
    new n/t3
    set n/t3 mul n/t1 n/t2
    new b/temp
    set b/temp le n/t3 0
    return b/temp

r/s/intToBaseNChar n/n
    # Returns the nth digit in base 36 or less (using capitalized digits).
    assertIsTrue intIsBetween n/n 0 36
    new s/res
    if le n/n 9
        set s/res strFromByte add n/n 48
    else
        set s/res strFromByte add n/n 55
    return s/res

r/n/intFromBaseNChar s/n
    # Returns an int given the nth digit in base 36 or less (using capitalized digits).
    assertIsChar s/n
    new n/res
    set n/res byteFromChar s/n
    if ge n/res 65
        set n/res sub n/res 55
    else
        set n/res sub n/res 48
    assertIsTrue intIsBetween n/res 0 36
    return n/res

r/s/intToBaseStr n/n n/b
    # Returns a string representing n in the requested base. Strategy based on https://www.geeksforgeeks.org/convert-base-decimal-vice-versa/
    new s/res
    while gt n/n 0
        set s/res cat s/res intToBaseNChar mod n/n n/b
        set n/n div n/n n/b
    set s/res reverseStr s/res
    return s/res
